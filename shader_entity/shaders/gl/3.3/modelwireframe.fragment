#version 330 core

out vec4 fragmentColor;

in vec3 viewDirection;
in vec3 normal;
in vec3 gnormal;
in vec3 worldPosition;
in vec3 worldWater;
in vec3 barycentric;
in vec3 gcolor;

uniform int lightingEnable;
uniform float transparency;

uniform vec3 minSpace;
uniform vec3 maxSpace;
//uniform float bottom; 
uniform float topVisibleHeight;
uniform float bottomVisibleHeight;
uniform float supportCos;

uniform int hoverState;
uniform int checkscope;

uniform int state;
uniform vec4 stateGain[4];
uniform vec4 outPlatformGain;
uniform vec4 insideGain;

uniform int nozzle;
uniform vec4 nozzleGain[16];

const vec3 wireframeColor = vec3(0.1);
const vec3 clearColor = vec3(0.27);
uniform int renderModel;

float brightness(vec3 color) {
    float bn = color.r * 0.3 + color.g * 0.59 + color.b * 0.11;
	return bn;
}

vec2 lightIntensity()
{
	vec2 intensity;
	float INTENSITY_CORRECTION = 0.6;

	/* 环境光 */
	vec3 LIGHT_TOP_DIR = vec3(-0.4574957, 0.4574957, 0.7624929);	
	float LIGHT_TOP_DIFFUSE = (0.8 * INTENSITY_CORRECTION);
	float LIGHT_TOP_SPECULAR  = (0.125 * INTENSITY_CORRECTION) * 2;
	float LIGHT_TOP_SHININESS = 10.0;

	/* 反射光 */
	// vec3 LIGHT_FRONT_DIR = vec3(0.6985074, 0.1397015, 0.6985074);
	vec3 LIGHT_FRONT_DIR = vec3(0.6985074, 0.1397015, 0.5985074);
	float LIGHT_FRONT_DIFFUSE = (0.3 * INTENSITY_CORRECTION) * 3;
	float LIGHT_FRONT_SPECULAR = (0.0 * INTENSITY_CORRECTION);
	float LIGHT_FRONT_SHININESS = 5.0;

	vec3 LIGHT_BACK_DIR = vec3(0.1397015, 0.6985074,0.6985074);
	float LIGHT_BACK_DIFFUSE = (0.3 * INTENSITY_CORRECTION);

	float INTENSITY_AMBIENT  =  0.3;

	vec3 eye_normal = normal;

	// Compute the cos of the angle between the normal and lights direction. The light is directional so the direction is constant for every vertex.
	// Since these two are normalized the cosine is the dot product. We also need to clamp the result to the [0,1] range.
	float NdotL = max(dot(eye_normal, LIGHT_TOP_DIR), 0.0);

	intensity.x = INTENSITY_AMBIENT + NdotL * LIGHT_TOP_DIFFUSE;
	vec3 position = worldPosition.xyz;
	intensity.y = LIGHT_TOP_SPECULAR * pow(max(dot(-normalize(position), reflect(LIGHT_TOP_DIR, eye_normal)), 0.0), LIGHT_TOP_SHININESS);

	// Perform the same lighting calculation for the 2nd light source (no specular applied).
	NdotL = max(dot(eye_normal, LIGHT_FRONT_DIR), 0.0);
	intensity.x += NdotL * LIGHT_FRONT_DIFFUSE;
	
	return intensity;
}

bool frontFacing()
{
    vec3 fdx = dFdx(worldPosition);
    vec3 fdy = dFdy(worldPosition);
    return dot(normalize(gnormal), normalize(cross(fdx, fdy))) > -0.05;
}

void main( void )
{
	if(checkscope > 0 && (worldPosition.z < bottomVisibleHeight || worldPosition.z > topVisibleHeight))
		discard;
	
	vec4 color = vec4(gcolor, 1.0);
	color = color * stateGain[state];

	vec3 fgnormal =	normalize(gnormal);
	
	if(worldPosition.x < minSpace.x || worldPosition.y < minSpace.y || worldPosition.z < minSpace.z 
		|| worldPosition.x > maxSpace.x || worldPosition.y > maxSpace.y || worldPosition.z > maxSpace.z)
	{
		color = color * outPlatformGain;
	}
	
	if(hoverState > 0)
	{
		float v = dot(fgnormal, vec3(0.0, 0.0, -1.0));
		float k = step(supportCos, v);
		color.r += 0.8 * v * k;
	}
	
	if(!frontFacing())
	{
		color = color * insideGain;
	}
	color = color * nozzleGain[nozzle];

	if (renderModel != 1) {
		vec3 baryDelta = fwidth(barycentric);
			
		vec3 edgeFactor = smoothstep(vec3(0.0), baryDelta * 1.0, barycentric);
		float minEdgeFactor = min(min(edgeFactor.x, edgeFactor.y), edgeFactor.z);

		if (renderModel == 2) {
			
			if (minEdgeFactor >= 1.0) {
                discard;
            }
            color.rgb = mix(wireframeColor, clearColor, minEdgeFactor);

		} else if (renderModel == 3) {
			color.rgb = mix(wireframeColor, color.rgb, minEdgeFactor);
		}
	}

	if (lightingEnable > 0)
	{
		vec3 coreColor = color.rgb;
		vec2 intensity = lightIntensity();	//获取 环境光x 和 反射光y 强度
		float bn = brightness(coreColor);	
		float gainFactor = pow((1 - bn * 0.14), 2);	// 根据颜色亮度添加增益，避免暗色系太暗，亮色系太亮。(b - bn * k)中b越大, 暗色系越亮, k越大, 亮色系越暗
		vec3 gain = vec3(gainFactor, gainFactor, gainFactor);
		coreColor = mix(coreColor, gain, 0.17);	
		fragmentColor = vec4(vec3(intensity.y) + (coreColor * intensity.x), transparency);
	}
	else 
	{
		fragmentColor = vec4(color.rgb, transparency);
	}
}
